// Standard Java Project ///////////////////////////////////////////////////////

subprojects {
	apply plugin: 'java'

	group = 'org.syncany'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

	repositories {
		mavenCentral()
		google()
	}

	sourceSets {
		main {
			java {
				srcDirs = ['src/main/java']
			}
			resources {
				srcDirs = ['src/main/resources']
			}
		}

		test {
			java {
				srcDirs = ['src/test/java']
			}
			resources {
				srcDirs = ['src/test/resources']
			}
		}

		integration {
			java {
				srcDirs = ['src/it/java']
			}
			resources {
				srcDirs = ['src/it/resources']
			}
			compileClasspath += sourceSets.test.compileClasspath
			runtimeClasspath += sourceSets.test.runtimeClasspath
			output.classesDirs.setFrom(sourceSets.test.output.classesDirs)
		}
	}

	dependencies {
		integrationImplementation sourceSets.test.output
	}

	processResources {
		duplicatesStrategy = DuplicatesStrategy.INCLUDE

		from(source) {
			include "**/*.properties", "**/*.xml", "**/*.skel"
			filter { String line ->
				line.replaceAll('%applicationRelease%', "${applicationRelease}")
			}
			filter { String line ->
				line.replaceAll('%applicationVersion%', "${applicationVersion}")
			}
			filter { String line ->
				line.replaceAll('%applicationVersionFull%', "${applicationVersionFull}")
			}
			filter { String line ->
				line.replaceAll('%applicationRevision%', "${applicationRevision}")
			}
			filter { String line ->
				line.replaceAll('%applicationDate%', "${asUTC(applicationDate, 'EEE, dd MMM yyyy HH:mm:ss Z')}")
			}
		}
	}

	// JAR /////////////////////////////////////////////////////////////////////

	jar {
		archiveBaseName = "${project.name}"
		archiveVersion = "$applicationVersionFull"
	}
}

